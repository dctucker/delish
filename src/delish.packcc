%prefix "deli"

%auxil "struct deli_t *"

%header {
	#include "stdarg.h"
	#include "delikind.h"
	struct deli_t {
		const char *input;
		size_t offset;
		size_t length;
		void *parser;
	};
	
	#define PCC_DEBUG(auxil, event, rule, level, pos, buffer, length) \
		if( !( dk##rule == dkComment || dk##rule == dkVLine || dk##rule > dkDefaultOp ) ) \
			deli_event(auxil->parser, event, dk##rule, level, pos, buffer, length)
	#define PCC_GETCHAR(auxil) deli_get_character( auxil )
	#define PCC_BUFFERSIZE 1024
	#define PCC_ERROR(auxil) pccError(auxil); return 0;

	#define BB(C,I)     bracket( auxil->parser, _0s, C, I )
	#define NS(K)       __ = nodeString( auxil->parser, dk##K   , _1s, _1e, _1)
	#define CN(N, K, ...) __ = createNode##N( auxil->parser, dk##K, __VA_ARGS__ )
	#define CN0(K) __ = createNode0( auxil->parser, dk##K )
	#define CN1(K, ...) CN(1, K, __VA_ARGS__ )
	#define CN2(K, ...) CN(2, K, __VA_ARGS__ )
	#define CN3(K, ...) CN(3, K, __VA_ARGS__ )
	#define NA(P,S)     nodeAppend( auxil->parser, P, S )
	#define SL(X)       setLine( auxil->parser, X, _0s )
	#define SLE(X)      setLine( auxil->parser, X, _0e )
	#define SL1(X)      setLine( auxil->parser, X, _1s )
	#define SL2(X)      setLine( auxil->parser, X, _2s )
	#define SL3(X)      setLine( auxil->parser, X, _3s )
	#define SL4(X)      setLine( auxil->parser, X, _4s )
	#define SL5(X)      setLine( auxil->parser, X, _5s )
	#define SL6(X)      setLine( auxil->parser, X, _6s )
	#define ER(MSG)     parserError( auxil->parser, _0s, MSG );
	// fprintf(stderr, "Parsing error: %s\n", MSG)

	#define GET_DK( _1, _2, _3, _4, NAME, ...) NAME
	#define DK(...) GET_DK( __VA_ARGS__, CN3, CN2, CN1, CN0)(__VA_ARGS__)

	void pccError(void*);
	int nodeAppend(void*, int, int);
	int createNode0(void*, int);
	int createNode1(void*, int, int);
	int createNode2(void*, int, int, int);
	int createNode3(void*, int, int, int, int);
	int nodeString(void*, int, size_t, size_t, char*);
	int bracket(void*, int, char, char);
	int parserError(void*, size_t, char*);
	int setLine(void*, int, int);
	void deli_event(void*, int, int, int, size_t, char*, size_t);

	const char
		*expected_condition  = "expected condition",
		*expected_expression = "expected expression",
		*invalid_expression  = "invalid expression",
		*expected_body       = "expected body",
		*expected_variable   = "expected variable",
		*expected_invocation = "expected invocation";
}

%source {
	int deli_get_character( struct deli_t *auxil )
	{
		if( auxil->offset >= auxil->length )
			return -1;
		return (auxil)->input[ auxil->offset++ ];
	}
}

Script        <- c:Code                                              { DK( Script, c ); }

Code          <-                                                     { DK( Code ); }
              (  S* l:VLine                                          { if( l >= 0 ) NA( $$, l ); }
              )+ S*

Body          <- ( "{"                                               { BB('{', 1); }
                    _ < c:Code >                                    ~{ ER( expected_body ); }
                                 _ "}"                              ~{ ER("missing `}`"); }
                 )                                                  ~{ ER( expected_body ); }
                                                                     { BB('{', -1); $$ = c; SL1(c); }

S             <- ( "\\" "\n" ) / "\t" / ' '

VLine         <-
              (  ( "\n" / ";" )                                      { $$ = -1; }
              /  Comment                                             { $$ = -1; }
              /  a:Block                                             { $$ = a; }
              /  a:Statement S* Comment* S* ( "\n" / ";" )           { $$ = a; }
              /  e:Expr                                              { $$ = e; }
              )                                                      { if( $$ >= 0 ) SL($$); }

Comment       <- '#' (! "\n" .)* "\n"
Block         <-
              (  !'$'
               ( &"if"    a:Conditional
               / &"do"    a:DoLoop
               / &"while" a:WhileLoop
               / &"for"   a:ForLoop
               / a:Function
               )
              /  a:Subshell
              )                                                      { DK( Block    , a ); SL(a); }

Statement     <-
              (  a:VariableStmt / a:LocalStmt   / a:ArgStmt
              /  a:EnvStmt      / a:IncludeStmt / a:StreamStmt
              /  a:CloseStmt    / a:RunStmt
              /  a:FunctionStmt / a:BreakStmt
              /  a:ContinueStmt / a:ReturnStmt  / a:Push / a:Pop )   { DK( Statement  , a ); SL(a); }

Conditional   <- "if"        S+ < e:Condition >                     ~{ ER( expected_condition ); }
                             S+   c:Body                             { DK( Conditional, e, c ); SL1(e); }
              (  S+ "elif"   S+ < e:Condition >                     ~{ ER( expected_condition ); }
                             S+   c:Body                             { NA( $$, e ); NA( $$, c ); SL2(e); }
              )* (           S+ < e:Else      >                      { NA( $$, e ); SL3(e); }
                             S+   c:Body                             { NA( $$, c ); }
              )? _

Else          <- "else"                                              { DK( Else ); }

WhileLoop     <- "while"   S+ e:Condition                           ~{ ER( expected_condition ); }
                           S+ c:Body _                               { DK( WhileLoop  , e, c ); }

DoLoop        <- "do"      S+ c:Body                                ~{ ER( expected_body ); }
                 _ "while" S+ e:Condition                           ~{ ER( expected_condition ); }
                                                                     { DK( DoLoop     , c, e ); }

ForLoop       <- "for"     S+ v:Variable                            ~{ ER( expected_variable ); }
                           S+ "in"                                  ~{ ER("expected `in`"); }
                           S+ e:IterExpr                            ~{ ER("expected iterable"); }
                           S+ c:Body                                ~{ ER( expected_body ); }
                           _                                         { DK( ForLoop    , v, e, c ); }

Function      <- !Keyword i:Identifier S* "="
                                       S* c:Body                    ~{ ER( expected_body ); }
                                       _                             { DK( Function   , i, c ); }

Subshell      <- "sub" S+ c:Body                                    ~{ ER( expected_body ); }
                       _                                             { DK( Subshell   , c ); }

BreakStmt     <- "break"                                             { DK( BreakStmt ); }
ContinueStmt  <- "continue"                                          { DK( ContinueStmt ); }

ReturnStmt    <- "return"                                            { DK( ReturnStmt ); }
              (  S+ e:Expr                                          ~{ ER( invalid_expression ); }
                                                                     { NA( $$, e ); }
              )?

Push          <- "push"                                              { DK( Push ); }
Pop           <- "pop"                                               { DK( Pop ); }

VariableStmt  <- &'$' v:Variable                                    ~{ ER("invalid variable name"); }
              (  S* o:AssignOp S* e:AssignExpr S*                   ~{ ER( invalid_expression ); }
              /  S* o:AppendOp S* e:AppendExpr S*                   ~{ ER( invalid_expression ); }
              /  S* o:RemoveOp S* e:AppendExpr S*                   ~{ ER( invalid_expression ); }
              )                                                      { DK( VariableStmt, v, o, e ); }

AssignExpr    <-
              (  e:OpenExpr / e:ArgExpr /  e:RunStmt
              /  e:FunctionCall / e:Cast / e:Expr
              )                                                      { $$ = e; }

AppendExpr    <-
              (   e:ArgExpr / e:Arg / e:VarDeref / e:ConstExpr
              /   e:Cast
              )                                                      { $$ = e; }

OpenExpr      <- "open" (S+ o:RedirOp)? S+ ( p:Path / p:VarDeref )  ~{ ER("expected path after `open`"); }
                                                                     { DK( OpenExpr, p, o); }
CloseStmt     <-
              (  "close" S+ v:Variable                              ~{ ER( expected_variable ); }
              /  v:Variable ".close"
              )                                                      { DK( CloseStmt, v ); }

LocalStmt     <- "local" S+ v:Variable                              ~{ ER( expected_variable ); }
                                                                     { DK( LocalStmt, v ); }
              (  S* o:AssignOp S* e:AssignExpr                      ~{ ER("expected assignable expression after `=`"); }
                                                                     { NA( $$, o ); NA( $$, e ); }
              )?

ArgStmt       <- "arg"
              (  a:ArgNames S* o:DefaultOp S* d:ArgDefault           { DK( ArgStmt, a, o, d); }
              /  a:ArgNames                                          { DK( ArgStmt, a); }
              /  S+ v:Variable                                       { DK( ArgStmt, v); }
              )                                                     ~{ ER("argument must be flags or a variable") }

EnvStmt       <- "env" S+ v:Variable                                ~{ ER( expected_variable ); }
                                                                     { DK( EnvStmt, v ); }
              (  S* o:DefaultOp S* e:EnvDefault                     ~{ ER("expected constant expression after `|=`"); }
                                                                     { NA( $$, o ); NA( $$, e ); }
              /  S* o:AssignOp  S* e:Expr                           ~{ ER( expected_expression ); }
                                                                     { NA( $$, o ); NA( $$, e ); }
              )?

IncludeStmt   <- "include" S+ s:StrLiteral                          ~{ ER("expected string literal after `include`"); }
                                                                     { DK( IncludeStmt, s ); }

StreamStmt    <-
              (  v:Variable "." s:Stream S+ l:ExprList              ~{ ER("expected streamable expression"); }
                                                                     { DK( StreamStmt,  v, s, l ); }
              /                 s:Stream S+ l:ExprList              ~{ ER("expected streamable expression"); }
                                                                     { DK( StreamStmt,  s, l ); }
              )

RunStmt       <-
              (  f:RunFlags S+ "run"                                 { DK( RunStmt, f ); }
              /  "run"                                               { DK( RunStmt ); }
              )  S+ i:Invocation                                    ~{ ER("expected invocation"); }
                                                                     { NA( $$, i ); }
              (  S* "|" S* i:Invocation                             ~{ ER("expected invocation"); }
                                                                     { NA( $$, i ); }
              )*

RunFlags      <-
              (  f:AsyncFlag                                         { DK( RunFlags, f ); }
              /  f:RedirFlag                                         { DK( RunFlags, f ); }
              /  (f1:AsyncFlag f2:RedirFlag)                         { DK( RunFlags, f1, f2 ); }
              )

FunctionStmt  <- !Keyword c:FunctionCall                             { DK( FunctionStmt, c ); SL(c); }
FunctionCall  <- !Keyword
              (  !Type i:Identifier                                  { DK( FunctionCall, i ); }
              /  !Type v:Variable '.' i:Identifier                   { DK( FunctionCall, DK( VarDeref, v ), i ); }
              /        t:Type '.' i:Identifier                       { DK( FunctionCall, DK( Type, t ), i); }
              )
              (  S+ e:Expr                                           { NA( $$, e ); }
              )*

ArgDefault    <- e:ConstExpr                                         { DK( ArgDefault, e ); }
EnvDefault    <- e:ConstExpr                                         { DK( EnvDefault, e ); }

ExprList      <- e:Expr                                              { DK( ExprList, e ); }
              (  S* ',' S* e:Expr                                   ~{ ER( expected_expression ); }
                 S*                                                  { NA( $$, e ); }
              )*

Invocation    <- c:Command                                           { DK( Invocation, c ); }
              (  S+ !'|' (e:Expr / e:String)                         { NA( $$, e ); }
              )*

ArgExpr       <- a:Arg (S+ / '=') e:Expr?                            { DK( ArgExpr, a, e ); }

ConstExpr     <-
              (  e:Number / e:StrBlock / e:StrLiteral
              /  e:Boolean / e:Array / e:Object
              /  e:Path / e:JsonBlock
              )                                                      { $$ = e; }

Expr          <-
              (  [(] S* s:MathExpr S* [)]
              /  s:Array / s:Object
              /  s:Arg / s:VarDeref
              /  s:StrBlock / s:StrLiteral / s:Regex
              /  s:DateTime / s:Number / s:Boolean
              /  s:BoolExpr
              /  s:Path     / s:JsonBlock  / s:Stream
              /  s:Cast     / s:FunctionCall
              )                                                      { DK( Expr, s); }

IterExpr      <- ( s:Array / s:VarDeref )                            { $$ = s; }

Condition     <- a:OrExpr                                            { $$ = a; }
OrExpr        <- l:OrExpr  S+ o:BoolOr  S+ r:AndExpr                 { DK( CondExpr, o, l, r); }
               /                           r:AndExpr                 { $$ = r; }
AndExpr       <- l:AndExpr S+ o:BoolAnd S+ r:NotExpr                 { DK( CondExpr, o, l, r); }
               /                           r:NotExpr                 { $$ = r; }
NotExpr       <- o:BoolNot S+ r:CondExpr                             { $$ = o; NA($$, r); }
               /              r:CondExpr                             { $$ = r; }
CondExpr      <-
              (  s:Arg / s:BoolExpr
              /  s:FunctionCall
              /  s:VarDeref / s:Boolean
              )                                                      { $$ = s; }

MathExpr      <- a:AddExpr                                           { $$ = a; }
AddExpr       <- l:AddExpr S+ ( o:AddOp / o:SubOp ) S+ r:MulExpr     { DK( MathExpr, o, l, r ); }
               /                                       r:MulExpr     { $$ = r; }
MulExpr       <- l:MulExpr S+ (o:MulOp / o:DivOp / o:ModOp)
                           S+ r:PrimExpr                             { DK( MathExpr, o, l, r ); }
               /              r:PrimExpr                             { $$ = r; }
PrimExpr      <- e:Expr                                              { $$ = e; }

MulOp         <- "*"                                                 { DK( MulOp ); }
DivOp         <- "/"                                                 { DK( DivOp ); }
ModOp         <- "%"                                                 { DK( ModOp ); }
AddOp         <- "+"                                                 { DK( AddOp ); }
SubOp         <- "-"                                                 { DK( SubOp ); }

BoolExpr      <-
              (      n:BoolNot S+ c:Comparison                       { DK( BoolExpr, n ); NA(n, c); }
               /                  c:Comparison                       { DK( BoolExpr, c ); }
              )(  S+ o:BoolOp2 S+ b:BoolExpr                         { DK( BoolExpr, o, $$, c); }
              )*

BoolOp2       <- o:BoolOr / o:BoolAnd                                { DK( BoolOp2, o ); }
BoolNot       <- "not"                                               { DK( BoolNot ); }
BoolOr        <- "or"                                                { DK( BoolOr ); }
BoolAnd       <- "and"                                               { DK( BoolAnd ); }

Comparison    <- e1:CompExpr S+ o:CompOp S+ e2:CompExpr             ~{ ER("expected comparable expression"); }
                                                                     { DK( Comparison, o, e1, e2 ); }
CompExpr      <-
              (  s:FunctionCall / s:VarDeref / s:StrBlock / s:StrLiteral / s:Regex
              /  s:Number / s:Boolean / s:Path / s:Arg
              )                                                      { $$ = s; }
CompOp        <-
              (  o:CompGe / o:CompGt / o:CompLe / o:CompLt
              /  o:CompEq / o:CompNe / o:CompMatch
              )                                                      { $$ = o; }
CompGe        <- ">="                                                { DK( CompGe ); }
CompGt        <- ">"                                                 { DK( CompGt ); }
CompLe        <- "<="                                                { DK( CompLe ); }
CompLt        <- "<"                                                 { DK( CompLt ); }
CompEq        <- "=="                                                { DK( CompEq ); }
CompNe        <- "!="                                                { DK( CompNe ); }
CompMatch     <- "=~"                                                { DK( CompMatch ); }

VarDeref      <- &'$' n:Variable                                     { DK( VarDeref, n ); }
              (  "."
               ( s:StrLiteral / s:Integer
               / s:Variable   / s:Identifier  )                     ~{ ER("expected literal, variable or identifier after `.`"); }
                                                                     { NA( $$, s ); }
              )*

RedirFlag     <- "redir"                                             { DK( RedirFlag ); }
              (  S+
               ( i:VarDeref / i:Path / i:Stream )                   ~{ ER("expected redirection source"); }
                 S* r:RedirOp S*                                    ~{ ER("expected redirection operator"); }
               ( o:VarDeref / o:Path / o:Stream )                   ~{ ER("expected redirection destination"); }
                                                                     { NA( r, i ); NA( r, o ); NA( $$, r ); }
              )+

AsyncFlag     <- "async"                                             { DK( AsyncFlag ); }

ArgNames      <- S+ a:Arg                                            { DK( ArgNames, a); }
              (  S+ a:Arg                                            { NA( $$, a); }
              )*

Arg           <- &'-'
              (  l:ArgLong                                           { DK( Arg, l ); }
              /  s:ArgShort                                          { DK( Arg, s ); }
              )                                                     ~{ ER("invalid argument name"); }

Array         <- "[" S* "]"                                          { DK( Array ); }
              /  '['                                                 { DK( Array ); }
              (      _  e:Expr                                       { NA( $$, e ); }
                     S*       ','? _
              )* ']'

Object        <- '['                                                 { DK( Object ); }
              (      _  p:Pair                                       { NA( $$, p ); }
                     S*       ','? _
              )+ ']'

Pair          <-
                 i:Identifier S* ":" S* c:Body                      ~{ ER("expected body after `:`"); }
                                                                     { DK( Pair, i, c ); }
              /
                 k:KeyExpr    S* ':' S* v:Expr                      ~{ ER("expected value expression"); }
                                                                     { DK( Pair, k, v ); }
KeyExpr       <-
              (
                 k:Variable / k:StrLiteral / k:Integer
              )                                                      { $$ = k; }

JsonBlock     <- "json" S+ s:StrBlock                               ~{ ER("expected string literal block after `json`"); }
                                                                     { DK( JsonBlock, s ); }

Keyword       <- ( "json" / "sub"   / "if" / "elif" / "else"
              /  "do" / "while" / "for"
              /  "in"   / "out"   / "err" / "include"
              /  "true" / "false" / "env" / "arg" / "local"
              /  "return" / "break" / "continue"
              /  "async" / "redir" / "and" / "or" / "not"
              /  "push" / "pop" / "open" / "close" / "run" )

Cast          <- t:Type S* "(" S* e:Expr                            ~{ ER("expected expression"); }
                               S*        ")"                         { DK( Cast, t, e ); }

Type          <- "Integer"                                           { DK( Integer    ); }
              /  "Decimal"                                           { DK( Decimal    ); }
              /  "DateTime"                                          { DK( DateTime   ); }
              /  "Arg"                                               { DK( Arg        ); }
              /  "Array"                                             { DK( Array      ); }
              /  "Boolean"                                           { DK( Boolean    ); }
              /  "Identifier"                                        { DK( Identifier ); }
              /  "Path"                                              { DK( Path       ); }
              /  "Object"                                            { DK( Object     ); }
              /  "Regex"                                             { DK( Regex      ); }
              /  "Stream"                                            { DK( Stream     ); }
              /  "String"                                            { DK( String     ); }
              /  "Variable"                                          { DK( Variable   ); }

StrBlock      <- "\"\"\"" < ( !"\"\"\"" . )* > "\"\"\""              { NS( String ); }
StrLiteral    <- s1:SingleQuote                                      { $$ = s1; }
              /  s2:DblQuote                                         { $$ = s2; }
DblQuote      <- ( "\"" <              [^"]*    > "\"" )             { NS( String ); }
SingleQuote   <- ( "'"  <              [^']*    >  "'" )             { NS( String ); }
Command       <-        <   W ( W / '-'   )*    >                    { NS( String ); }
Identifier    <-        <   A ( W / '-'   )*    >                    { NS( Identifier); }
ArgShort      <-   '-'  <       W+              >                    { NS( ArgShort ); }
ArgLong       <-  "--"  <     ( W ( '-' W )* )+ >                    { NS( ArgLong ); }
String        <-        <       C+              >                    { NS( String ); }
Regex         <-  'r/'  <    ( '\/' / !'/' . )+ > '/'                { NS( Regex ); }

Number        <- d:Decimal                                           { $$ = d; }
               / i:Integer                                           { $$ = i; }
Decimal       <- < [0-9]+ '.' [0-9]+ >                               { NS( Decimal ); }

DateTime      <- d:Date [-Tt .:+@] t:Time                            { DK( DateTime, d, t ); }
Date          <- y:Year '-' m:Month  '-' d:Day                       { DK( Date, y, m, d ); }
Time          <- h:Hour ':' m:Minute ':'                             { DK( Time, h, m ); }
              (  s:Second '.' n:NanoSecond                           { NA( $$, s ); NA( $$, n ); }
              /  s:Second                                            { NA( $$, s ); }
              )  z:Timezone?                                         { NA( $$, z ); }
Year          <- <            [0-9] [0-9] [0-9] [0-9]       >        { NS( Integer ); }
Month         <- <                        [0-1] [0-9]       >        { NS( Integer ); }
Day           <- <                        [0-3] [0-9]       >        { NS( Integer ); }
Hour          <- <                        [0-2] [0-9]       >        { NS( Integer ); }
Minute        <- <                        [0-6] [0-9]       >        { NS( Integer ); }
Second        <- <                        [0-6] [0-9]       >        { NS( Integer ); }
NanoSecond    <- <                              [0-9]+      >        { NS( NanoSecond ); }
Timezone      <- <  [-+] [0-2] [0-9] ':'? [0-6] [0-9] / 'Z' >        { NS( Timezone ); }

Integer       <-  &"0x"  h:Int16                                     { $$ = h; }
              /   &'0'   o:Int8                                      { $$ = o; }
              /   !'0'   d:Int10                                     { $$ = d; }
Int16         <- < "0x" [0-9A-Fa-f]+ >                               { NS( Integer ); }
Int8          <- < '0' !'x' [0-7]* >                                 { NS( Integer ); }
Int10         <- < [0-9]+ >                                          { NS( Integer ); }

Path          <- < ("."* "/" C+ ) / "." >                            { NS( Path ); }
Variable      <- '$' < W (W / '-')* >                                { NS( Variable ); }
Boolean       <- < "true" / "false" >                                { NS( Boolean ); }
Stream        <- &[ioe] ( s:StreamIn / s:StreamOut / s:StreamErr )   { DK( Stream, s ); }
StreamIn      <- "in"                                                { DK( StreamIn ); }
StreamOut     <- "out"                                               { DK( StreamOut ); }
StreamErr     <- "err"                                               { DK( StreamErr ); }
AssignOp      <- "="                                                 { DK( AssignOp ); }
AppendOp      <- "+="                                                { DK( AppendOp ); }
RemoveOp      <- "-="                                                { DK( RemoveOp ); }
RedirOp       <-
              (  o:RedirAppendOp / o:RedirReadOp
              /  o:RedirWriteOp / o:RedirDuplexOp
              )                                                      { DK( RedirOp, o ); }
RedirAppendOp <- ">>"                                                { DK( RedirAppendOp ); }
RedirReadOp   <- "<"                                                 { DK( RedirReadOp ); }
RedirWriteOp  <- ">"                                                 { DK( RedirWriteOp ); }
RedirDuplexOp <- "<>"                                                { DK( RedirDuplexOp ); }
DefaultOp     <- "|="                                                { DK( DefaultOp ); }
C             <- [^ \n\r\t;]
A             <- [A-Za-z]
W             <- [A-Za-z0-9_]
_             <- [ \n\r\t;]*

%%

int packcc_main(const char *input, int len, void *p)
{
	struct deli_t auxil = {
		input: input,
		offset: 0,
		length: len,
		parser: p,
	};
	deli_context_t *ctx = deli_create(&auxil);
	while(deli_parse(ctx, NULL));
	deli_destroy(ctx);
	return 0;
}
